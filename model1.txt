
model{
  # Prior distributions
  beta0 ~ dunif(0,10)   # Intercept of lambda
  alpha0 ~ dunif(-10,10)  # Intercept of log(sigma)
  log(sigma) <- alpha0

  # psi is a derived parameter under DA for stratified populations
  psi <- sum(lambda[1:nsites]) / (nind + nz)

  # 'Likelihood'
  for(i in 1:(nind + nz)){                          # i is index for individuals
    z[i] ~ dbern(psi)                               # Data augmentation variables
    d[i] ~ dunif(0, B)                              # distance uniformly distributed
    p[i] <- exp(-d[i] * d[i] / (2 * sigma * sigma)) # Detection function
    y[i] ~ dbern(z[i] * p[i])                       # Basic Bernoulli random variable
    site[i] ~ dcat(site.probs[1:nsites])            # Population distribution among sites
  }

  # Linear models for abundance and for detection
  for(s in 1:nsites){                               # s is index for sites
    # Model for abundance
    N[s] ~ dpois(lambda[s])                         # Realized abundance at site s
    log(lambda[s]) <- beta0                         # same lambda at all site
    site.probs[s] <- lambda[s] / sum(lambda[1:nsites])

    # detection
    # log(sigma[s]) <- alpha0
  }

  # Derived parameter: total population size across all sites
  Ntotal <- sum(z[1:(nind + nz)])
  area <- nsites * 2 * B * transect_len
  D <- Ntotal / area
  N_gic <- D * size_study_area
}


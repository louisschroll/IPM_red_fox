
model {
  # Model for initial state
  N[1,1] <- 1
  N[2,1] <- 1

  # Loop over time
  for (t in 1:T){
    # Population model
    N[1,t+1] <- sj[t] * (f1[t] * N[1,t] + fa[t] * N[2,t])
    N[2,t+1] <- sa[t] * (N[1,t] + N[2,t])

    # Annual (realized) growth rate on log scale
    r.annual[t] <- log(N[1,t+1] + N[2,t+1]) - log(N[1,t] + N[2,t])
  }
  r <- mean(r.annual[u:T])
  lambda <- exp(r)

  # Sensitivity and elasticity of lambda to changes in sj
  delta <- 0.001                                                # size of perturbation
  N.star[1,1] <- 1
  N.star[2,1] <- 1
  for (t in 1:T){
    N.star[1,t+1] <- (sj[t] + delta) * (f1[t] * N.star[1,t] + fa[t] * N.star[2,t])
    N.star[2,t+1] <- sa[t] * (N.star[1,t] + N.star[2,t])
    r.annual.star[t] <- log(N.star[1,t+1] + N.star[2,t+1]) - log(N.star[1,t] + N.star[2,t])
  }
  r.star <- mean(r.annual.star[u:T])
  s.sj <- (exp(r.star) - lambda) / delta
  e.sj <- s.sj * mean(sj) / lambda
}
